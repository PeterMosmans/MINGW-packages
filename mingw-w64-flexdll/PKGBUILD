# Maintainer: Junjie Mao <eternal.n08@gmail.com>


_realname=flexdll
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.35
pkgrel=1
pkgdesc="An implementation of a dlopen-like API for Windows (mingw-w64)"
arch=('any')
url='http://alain.frisch.fr/flexdll.html'
license=('zlib/libpng License')
makedepends=("${MINGW_PACKAGE_PREFIX}-ocaml")
source=("http://alain.frisch.fr/flexdll/${_realname}-${pkgver}.tar.gz"
        "0001-Fix-windres-path.patch"
        "0002-use-lib-flexdll-as-the-default-library-path.patch")
sha256sums=('125f1fa966228296c5b08ed04af3e6c10bbdc9829d5c4d094e3ad17adf898797'
            'b9b330f4519c53f1c6e7e3cbbd555696e4349ee6cc99be1c43d4a40a3f5b2ff6'
            'c5e6d9891557866d1dfaed3a693dfaa294babfbecec1970ac2f51ae9eabc10d8')

if [ "${MSYSTEM_CARCH}" == "i686" ]; then
  chain="mingw"
else
  chain="mingw64"
fi

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/0001-Fix-windres-path.patch
  patch -p1 -i ${srcdir}/0002-use-lib-flexdll-as-the-default-library-path.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf ${_realname}-${pkgver} build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  unset FLEXDIR
  unset OCAMLLIB
  make flexlink.exe -j1 TOOLCHAIN=${chain}
  make build_${chain} -j1 TOOLCHAIN=${chain}
}

check() {
  # The test demo works on x86_64 but fails on i686. Root causes of the failure
  # is still unknown, but the tool is good enough to be used in other
  # applications such as ocaml and coq.
  plain "skip"
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}

  install -Dm755 flexlink.exe ${pkgdir}${MINGW_PREFIX}/bin/flexlink.exe
  for f in flexdll_${chain}.o flexdll_initer_${chain}.o flexdll.h flexdll.c flexdll_initer.c default.manifest default_amd64.manifest; do
    install -D ${f} ${pkgdir}${MINGW_PREFIX}/lib/flexdll/${f}
  done
  [ -f LICENSE ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
  [ -f CHANGES ] && install -Dm644 CHANGES ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/CHANGES
}
